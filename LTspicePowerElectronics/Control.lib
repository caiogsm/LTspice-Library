** Continuous and discrete control blocks


******************************************************************
********* General transfer functions 
******************************************************************

.subckt Integrator In Out
G1 0 N001 In 0 1
C1 N001 0 1
R1 N001 N002 1 ; workaround to avoid a warning that node N001 is floating
E1 N002 0 N001 0 1 ; workaround
E2 Out 0 N001 0 {1/T}
.param T=1 InitVal=0
.ic V(N001)={InitVal*T}
.ends Integrator

.subckt Differentiator In Out
G1 0 N001 In 0 1
E1 Out 0 N001 0 1
L1 N001 0 {T} Rser=0
.param T=1
.ends Differentiator

******************************************************************
********* Continuous Controllers
******************************************************************
.subckt P_Controller In Out
E3 Out 0 In 0 {Kp}
.param Kp=1
.ends P_Controller

.subckt PI_Controller In Out
E1 Out1 0 N001 0 {Ki}
E2 N002 0 N001 0 1
C1 N001 0 1
R1 N002 N001 1
G1 0 N001 In 0 1
E3 Out2 0 In 0 {Kc}
B1 Out 0 V=V(Out2)+V(Out1)
.param Kc=1 wz=1000
.param Ki=wz*Kc
.ends PI_Controller

.subckt RES_Ideal_Controller In Out
G1 0 N001 In 0 1
L1 0 N001 {L1}
C1 0 N001 {C1}
E1 Out 0 N001 0 {Kr*C1}
.param Kr=1 fo=60
.param L1=1/(2*pi*fo) C1=1/(2*pi*fo) 
.ends RES_Ideal_Controller 

.subckt RES_NonIdeal_Controller In Out
E1 N001 0 In 0 1
E2 Out 0 N002 0 {Kr}
L1 N001 P001 {L1} Rser=0
C1 P001 N002 {C1}
R1 N002 0 {R1}
.param Kr=1 fo=60 fb=5
.param L1=1/(2*pi*fo) C1=1/(2*pi*fo) R1=fb/fo
.ends RES_NonIdeal_Controller 

.subckt PRES_Ideal_Controller In Out
G1 0 N001 In 0 1
L1 N001 0 {L1}
C1 0 N001 {C1}
E1 Out1 0 N001 0 {Kc*wx*C1}
E2 Out2 0 In 0 {Kc}
B1 Out 0 V=V(Out2)+V(Out1)
.param Kc=1 wx=1000 fo=60
.param L1=1/(2*pi*fo) C1=1/(2*pi*fo) 
.ends PRES_Ideal_Controller

.subckt PRES_NonIdeal_Controller In Out
E1 N001 0 In 0 1
L1 N001 P001 {L1} Rser=0
C1 P001 N002 {C1}
R1 N002 0 {R1}
E2 Out1 0 N002 0 {Kr}
E3 Out2 0 In 0 {Kc}
B1 Out 0 V=V(Out2)+V(Out1)
.param Kc=1 Kr=1000 fb=5 fo=60
.param L1=1/(2*pi*fo) C1=1/(2*pi*fo) R1=fb/fo
.ends PRES_NonIdeal_Controller